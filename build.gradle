buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    alias(libs.plugins.release)
    alias(libs.plugins.versions)
}

ext {
    group = 'io.github.muehmar'
}

allprojects {
    group = rootProject.ext.group
}

tasks.register('build') {
    // Make the release plugin happy
}

release {
    preTagCommitMessage = 'Release '
    tagCommitMessage = 'Release '
    newVersionCommitMessage = 'Setting new version: '
    tagTemplate = 'v${version}'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
